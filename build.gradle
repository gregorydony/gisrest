apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

// war plugin
// See: http://www.gradle.org/docs/current/userguide/war_plugin.html
apply plugin: 'war'
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'

jar {
    baseName = 'gs-rest-service'
    version =  '0.1.0'
}

// Enable debugging of gretty started webapp in IDEA
// For debuging a gradle task, IDEA creates a gradle init script (in /tmp/ijinit#.gradle), that sets JVM arguments
// to enable remote debugging.
// Unfortunately by default that only works for gradle tasks that implement JavaForkOptions - which gretty doesn't do.
// So parse the JVM args that IDEA uses from that init script and set them as JVM args to be used by gretty:
def debugArgs = []
if (gradle.startParameter.initScripts) {
    def line = file(gradle.startParameter.initScripts[0]).readLines().findAll({x -> x ==~ /.*-agentlib:jdwp=.*/ })[0]
    if (line) {
        debugArgs = ((line =~ /.*'(.*)'.*/)[0][1] as String).split(' ')
    }
}


gretty {
    jvmArgs = [*debugArgs]
    httpPort = 8080
    contextPath = '/'
    servletContainer = 'tomcat8'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    // https://mvnrepository.com/artifact/com.google.guava/guava
    compile group: 'com.google.guava', name: 'guava', version: '11.0.2'
    // https://mvnrepository.com/artifact/javax.ws.rs/javax.ws.rs-api
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1'
    // https://mvnrepository.com/artifact/org.glassfish.jersey.core/jersey-common
    compile group: 'org.glassfish.jersey.core', name: 'jersey-common', version: '2.26'
    // https://mvnrepository.com/artifact/org.glassfish.jersey.core/jersey-server
    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: '2.26'
    // https://mvnrepository.com/artifact/org.glassfish.jersey.containers/jersey-container-servlet
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.26'

    // https://mvnrepository.com/artifact/org.glassfish.jersey.media/jersey-media-json-jackson
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.26-b03'

    // https://mvnrepository.com/artifact/org.glassfish.jersey.inject/jersey-hk2
    compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.26'
    // https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    // https://mvnrepository.com/artifact/javax.inject/javax.inject
    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    // https://mvnrepository.com/artifact/org.glassfish.jersey.test-framework.providers/jersey-test-framework-provider-grizzly2
    testCompile group: 'org.glassfish.jersey.test-framework.providers', name: 'jersey-test-framework-provider-grizzly2', version: '2.26'
}